plugins {
    id 'java'

    id 'eclipse'
    id 'idea'

    id 'com.github.hierynomus.license' version '0.11.0'
}

group = 'net.minecrell'
version = '1.0-SNAPSHOT'
description = 'TODO'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    author = 'Minecrell <https://github.com/Minecrell>'

    pluginPackage = 'net.minecrell.box'
    pluginClass = "${pluginPackage}.BoxPlugin"
    resourceTokens = [
            'Name': rootProject.name,
            'Description': description,
            'Version': version,
            'Author': author,
            'PluginClass': pluginClass.toString()
    ];
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public' }
}

dependencies {
    compile 'org.spigotmc:spigot-api:1.8.7-R0.1-SNAPSHOT'
}

processResources {
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: resourceTokens
}

license {
    header file('HEADER')
    include '**/*.java'

    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}
